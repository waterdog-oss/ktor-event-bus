group = "waterdog.mobi"
version = "0.10"

buildscript {
    ext.versions = [
            "kotlin" : "1.3.40",
            "junit"  : "5.4.1",
            "koin"   : "2.0.1",
            "jackson": "2.9.8",
            "exposed": "0.18.1"
    ]

    ext.deps = [
            "kotlin"      : [
                    "stdlib"    : [
                            "jdk": "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}"
                    ],
                    "reflect"   : "org.jetbrains.kotlin:kotlin-reflect:${versions.kotlin}",
                    "coroutines": "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.2.2"
            ],
            "jackson"     : [
                    "core"    : "com.fasterxml.jackson.core:jackson-core:${versions.jackson}",
                    "databind": "com.fasterxml.jackson.core:jackson-databind:${versions.jackson}",
                    "kotlin"  : "com.fasterxml.jackson.module:jackson-module-kotlin:${versions.jackson}",
                    "jsr310"  : "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${versions.jackson}"
            ],
            "koin"        : [
                    "core"   : "org.koin:koin-core:${versions.koin}",
                    "coreExt": "org.koin:koin-core-ext:${versions.koin}",
                    "test"   : "org.koin:koin-test:${versions.koin}"
            ],
            "junit"       : [
                    "jupiterApi"   : "org.junit.jupiter:junit-jupiter-api:${versions.junit}",
                    "jupiterParams": "org.junit.jupiter:junit-jupiter-params:${versions.junit}",
                    "jupiterEngine": "org.junit.jupiter:junit-jupiter-engine:${versions.junit}"
            ],
            "exposed"     : [
                    "core"      : "org.jetbrains.exposed:exposed-core:${versions.exposed}",
                    "dao"       : "org.jetbrains.exposed:exposed-dao:${versions.exposed}",
                    "jodatime"  : "org.jetbrains.exposed:exposed-jodatime:${versions.exposed}",
                    "jdbc"      : "org.jetbrains.exposed:exposed-jdbc:${versions.exposed}"
            ],
            "kluent"      : "org.amshove.kluent:kluent:1.47",
            "logback"     : "ch.qos.logback:logback-classic:1.2.3",
            "h2"          : "com.h2database:h2:1.4.197",
            "hikari"      : "com.zaxxer:HikariCP:2.7.8",
            "kafkaClients": "org.apache.kafka:kafka-clients:2.2.0",
            "rxJava2"     : "io.reactivex.rxjava2:rxjava:2.2.8"
    ]

    ext.isCiBuild = System.getenv("CI") == "true"

    repositories {
        mavenCentral()
        gradlePluginPortal()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "gradle.plugin.org.jlleitschuh.gradle:ktlint-gradle:5.0.0"
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
    }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: "java"
apply plugin: "kotlin"
apply plugin: "org.jlleitschuh.gradle.ktlint"

compileKotlin.kotlinOptions.jvmTarget = "1.8"
compileTestKotlin.kotlinOptions.jvmTarget = "1.8"

repositories {
    mavenCentral()
    jcenter()
}

test {
    useJUnitPlatform()
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

dependencies {
    implementation deps.kotlin.stdlib.jdk
    implementation deps.kotlin.reflect
    implementation deps.kotlin.coroutines
    implementation deps.logback
    implementation deps.exposed.core
    implementation deps.exposed.dao
    implementation deps.exposed.jodatime
    implementation deps.koin.core
    implementation deps.koin.coreExt
    implementation deps.kafkaClients
    implementation deps.rxJava2
    implementation deps.jackson.core
    implementation deps.jackson.databind
    implementation deps.jackson.kotlin
    implementation deps.jackson.jsr310

    testImplementation deps.junit.jupiterApi
    testImplementation deps.junit.jupiterParams
    testImplementation deps.kluent
    testImplementation deps.hikari
    testImplementation deps.h2
    testImplementation deps.koin.test

    testRuntimeOnly deps.exposed.jdbc
    testRuntimeOnly deps.junit.jupiterEngine
}